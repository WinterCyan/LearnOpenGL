#version 330 core

in VS_OUT {
    vec2 TexCoords;
    vec3 Norm;
    vec3 FragPos;
} fs_in;

out vec4 FragColor;
uniform sampler2D cubeTexture;
uniform samplerCube depthCube;
uniform vec3 lightPos;
uniform vec3 viewPos;
uniform float far_plane;

float shadowOrNot(vec3 fragPos){
    vec3 dire = fragPos - lightPos;
    float min_depth = texture(depthCube, dire).r;
    min_depth = min_depth * far_plane;
    float current_depth = length(dire);
    float bias = .05;
    float shadow = current_depth - bias > min_depth ? 1.0 : 0.0;

    return shadow;
}

void main() {
    vec3 texColor = texture(cubeTexture, fs_in.TexCoords).rgb;
    vec3 norm = normalize(fs_in.Norm);
    vec3 lightRay = normalize(lightPos - fs_in.FragPos); // from fragment to lightPos;
    vec3 viewRay = normalize(viewPos - fs_in.FragPos); // from fragment to viewer
    vec3 halfway = normalize(viewRay + lightRay);
    // ambient
    vec3 ambient_color = .3 * texColor;
    // diffuse
    float diffuse = max(dot(norm, lightRay), .0);
    vec3 diffuse_color = diffuse * texColor;
    // specular
    float specular = pow(dot(halfway, norm), 64.);
    vec3 specular_color = specular * texColor;
    // shadow or not
    float shadow = shadowOrNot(fs_in.FragPos);
    // total
    vec3 final_color = ambient_color + (1.0 - shadow) * (diffuse_color + specular_color);
    //vec3 final_color = ambient_color + shadow * (diffuse_color + specular_color);
    //vec3 final_color = ambient_color + diffuse_color + specular_color;
    FragColor = vec4(final_color, 1.0);
}
